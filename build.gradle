buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.0.0'
    }
}

plugins {
    id "io.micronaut.application" version '1.5.0'
    id "com.github.node-gradle.node" version "2.2.4"
}

repositories {
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/service/local/repositories/releases/content'
        mavenContent{
            releasesOnly()
        }
    }
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
        mavenContent{
            snapshotsOnly()
        }
    }
}

java {
    sourceCompatibility = JavaVersion.toVersion('16')
    targetCompatibility = JavaVersion.toVersion('16')
}

// Setup the java execution process
mainClassName = "com.jongsoft.finance.Application"
tasks.withType(JavaExec) {
    classpath += configurations.developmentOnly
    jvmArgs('-XX:TieredStopAtLevel=1', '-Dcom.sun.management.jmxremote')
}

// Setup for the NPM build for standalone application
node {
    version = '12.18.3'
    download = true
}

task buildElectron (type: NpxTask) {
    dependsOn build, npmInstall

    command = 'electron-builder'
    args = ["-w"]
    inputs.files('package.json', 'package-lock.json')
    inputs.dir(fileTree("node_modules").exclude(".cache"))
}

test {
    useJUnitPlatform()
}

// Setup the dependencies for the wrapper build
dependencies {
    implementation "com.jongsoft.finance:fintrack-api:$apiVersion"
    implementation "com.jongsoft.finance:fintrack-ui:$angularVersion"

    def jerseyVersion = '3.0.2'
    testImplementation "org.glassfish.jersey.core:jersey-client:$jerseyVersion"
    testImplementation "org.glassfish.jersey.media:jersey-media-json-jackson:$jerseyVersion"

    def junitJupiterVersion = '5.4.2'
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitJupiterVersion"
    testImplementation("org.assertj:assertj-core:$assertjVersion")
    testImplementation 'io.rest-assured:rest-assured:4.4.0'

    testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"
    implementation platform('org.testcontainers:testcontainers-bom:1.15.3') //import bom
    testImplementation('org.testcontainers:testcontainers')
    testImplementation('org.testcontainers:junit-jupiter')
}
